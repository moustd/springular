plugins {
    id 'org.springframework.boot' version '2.4.2'
    id "org.flywaydb.flyway" version "7.5.0"
}

apply plugin: 'java';
apply plugin: 'distribution';
apply plugin: 'io.spring.dependency-management';

configurations {
    swaggerCodegen
}


group = 'dion'
version = '1.0'
sourceCompatibility = '1.8'

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':server-shared')
    implementation project(':client')
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    runtimeOnly 'com.microsoft.sqlserver:mssql-jdbc'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    implementation 'org.springdoc:springdoc-openapi-ui:1.5.2'

    swaggerCodegen group: 'io.swagger.codegen.v3', name: 'swagger-codegen-cli', version: '3.0.24'
}

test {
    useJUnitPlatform()
}

jar {
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

distributions {
    main {
        contents {
            from 'jsw'
            from 'build/distributionPrep'
        }
    }
}

task prepareLibForDistribution(type: Copy, dependsOn: bootJar) {
    from 'build/libs'
    into 'build/distributionPrep/lib'
}

distZip.dependsOn(prepareLibForDistribution)

task swaggerCodegenTypescript(type: JavaExec) {
    onlyIf {
        file('../client-workspace/projects/lib-server-api/').exists() && file('../client-workspace/projects/lib-server-api/').listFiles().length == 0
    }
    classpath configurations.swaggerCodegen
    args 'generate',
            '-l', 'typescript-angular',
            '-i', '../api-docs/server.yaml',
            '-o', '../client-workspace/projects/lib-server-api/',
            '-c', './swagger-config.json'
}

task swaggerCodegenJava(type: JavaExec) {
    onlyIf {
        file('../server-java-api/').exists() && file('../server-java-api/').listFiles().length == 0
    }
    classpath configurations.swaggerCodegen
    args 'generate',
            '-l', 'java',
            '-i', '../api-docs/server.yaml',
            '-o', '../server-java-api/',
            '-c', './swagger-config.json'
}
